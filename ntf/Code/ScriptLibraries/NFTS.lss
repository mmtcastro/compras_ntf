'++LotusScript Development Environment:2:5:(Options):0:74
%REM
	Library NFTS
	Created Mar 19, 2018 by Ludimila Oliveira/TDec
	Description: Comments for Library
%END REM
Option Public
Option Declare


'++LotusScript Development Environment:2:5:(Forward):0:1
Declare Function RetiraSinais(texto As String) As String
Declare Function ConverteTexto ( ByVal Texto As String, ByVal Tamanho As Integer ) As String
Declare Sub NFTS(dc As NotesDocumentCollection)
Declare Function ckEmpresa (empresa As NotesDocument) As Variant
Declare Function GetDadosEmpresa(codigo As String) As Variant
Declare Function ConverteData ( ByVal Data As Variant, ByVal Tamanho As Integer ) As String
Declare Function ConverteValor ( ByVal Valor As Variant, ByVal Casas As Integer, ByVal Tamanho As Integer ) As String

'++LotusScript Development Environment:2:5:(Declarations):0:2

'++LotusScript Development Environment:2:1:RetiraSinais:1:8
Function RetiraSinais(texto As String) As String
	
	Dim i As Integer
	Dim NovoTexto As String
	Dim size As Long
	Texto = UCase(Texto)
	Size = Len(Texto)
	For i = 1 To Size								'Retira caracteres inválidos
		Select Case Mid(Texto, i, 1)
		Case "."
		Case "-"
		Case "/"
		Case "$"
		Case ":"
		Case ";"
		Case " "
		Case Else
			NovoTexto = NovoTexto + Mid(Texto, i, 1)
		End Select
	Next
	RetiraSinais = NovoTexto
	
End Function


'++LotusScript Development Environment:2:1:ConverteTexto:1:8
Function ConverteTexto ( ByVal Texto As String, ByVal Tamanho As Integer ) As String
'Converte o Texto entrado, de forma que ele se adeque ao formato apropriado. Caso o Texto não possua o 
'Tamanho entrado, espaços em branco são acrescidos ao final do Texto, até que o tamanho seja atingido
'Remove os acentos e "tils" caso existam e retorna o Texto formatado.
	
	Dim i As Integer
	Dim Size As Integer
	Texto = UCase ( Texto )
	Size = Len ( Texto )
	For i = 1 To Size								'Substitui caracteres inválidos
		Select Case Mid ( Texto, i, 1 )
		Case "Á"
			Mid ( Texto, i, 1 ) = "A"
		Case "Â"
			Mid ( Texto, i, 1 ) = "A"
		Case "À"
			Mid ( Texto, i, 1 ) = "A"
		Case "Ã"
			Mid ( Texto, i, 1 ) = "A"
		Case "É"
			Mid ( Texto, i, 1 ) = "E"
		Case "Ê"
			Mid ( Texto, i, 1 ) = "E"
		Case "Í"
			Mid ( Texto, i, 1 ) = "I"
		Case "Ó"
			Mid ( Texto, i, 1 ) = "O"
		Case "Ô"
			Mid ( Texto, i, 1 ) = "O"
		Case "Õ"
			Mid ( Texto, i, 1 ) = "O"
		Case "Ú"
			Mid ( Texto, i, 1 ) = "U"
		Case "Ç"
			Mid ( Texto, i, 1 ) = "C"
		Case "º"
			Mid ( Texto, i, 1 ) = " "
		Case "'"
			Mid ( Texto, i, 1 ) = " "
		Case "´"
			Mid ( Texto, i, 1 ) = " "
		Case "`"
			Mid ( Texto, i, 1 ) = " "
		Case "&"
			Mid ( Texto, i, 1 ) = "E"
		Case "$"
			Mid ( Texto, i, 1 ) = " "
		Case ","
			Mid ( Texto, i, 1 ) = " "
		Case Chr(10)	'TAB
			Mid ( Texto, i, 1 ) = " "
		End Select
	Next
	
	If Size <=Tamanho Then
		i = Size + 1
		For i = i To Tamanho
			Texto = Texto + " "	
		Next
	Else
		MsgBox ("Estouro. Texto entrado é maior que o tamanho especificado. -> " + Texto)
		Stop
		ConverteTexto = ""
		Exit Function
	End If
	ConverteTexto = Texto
	
End Function


'++LotusScript Development Environment:2:2:NFTS:5:8
%REM
	Sub NFTS
	Description: Comments for Sub
%END REM
Sub NFTS(dc As NotesDocumentCollection)
	
	Dim ws As New NotesUIWorkspace
	Dim session As New NotesSession
	Dim doc As NotesDocument
	Set doc = dc.GetFirstDocument
	If doc Is Nothing Then Exit Sub
	
	Dim arqName As String
	Dim FileNumber As Integer
	Dim Buffer As String
	Dim Tamanho As Long, cont As Integer, scan As Integer, tot As Double
	FileNumber = FreeFile ( )
	arqName = "C:\NFTS\" + doc.CodEmpresa(0) + "\NFTS" + ConverteData(Today, 6) + ".TXT"
	Open ArqName For Output As FileNumber
	Dim dataI As New NotesDateTime(doc.DataEntrega(0))
	Dim dataF As New NotesDateTime(doc.DataEntrega(0))
	Dim tomadorServico As Variant, prestadorServico As Variant
	tomadorServico = GetDadosEmpresa(doc.CodEmpresa(0))
	Do Until doc Is Nothing
		'Define Data Inicial e Final
		Dim data As New NotesDateTime(doc.DataEntrega(0))
		If CDat(data.DateOnly) < CDat(dataI.DateOnly) Then
			Set dataI = New NotesDateTime(doc.DataEntrega(0))
		End If
		If CDat(data.DateOnly) > CDat(dataF.DateOnly) Then
			Set dataF = New NotesDateTime(doc.DataEntrega(0))
		End If
		Set doc = dc.GetNextDocument(doc)
	Loop
	
	Buffer = ""																						'Limpando a variável
	'********************************* Registro Header ************************************************************************************
	Buffer = "1"																					'Tipo de Registro
	Buffer = Buffer + "001"																			'Versão do Arquivo
	Buffer = Buffer + ConverteValor (RetiraSinais(tomadorServico(13)), 0, 8)						'Inscrição Municipal
	Buffer = Buffer + ConverteData(dataI.DateOnly, 8)												'Data Inicial
	Buffer = Buffer + ConverteData(dataF.DateOnly, 8)												'Data Final
	Buffer = Buffer + ConverteTexto ( "", 2 )														'Fim de Linha
	Print #FileNumber, Buffer																		'Salva registro no arquivo
	Tamanho = Len( Buffer ) 																		'Contabiliza tamanho total do Buffer
	'*******************************************************************************************************************************************
	If Tamanho <> 30 Then GoTo fim
	
	Set doc = dc.GetFirstDocument
	Do Until doc Is Nothing
		prestadorServico = GetDadosEmpresa(doc.CodCliente(0))
		
		Buffer = ""																					'Limpando a variável
		'********************************* Registro Detalhe - Tipo 4 ***************************************************************************
		Buffer = "4"																				'Tipo de Registro
		Buffer = Buffer + doc.TipoDocumento(0)														'Tipo de Documento (01 - Dispensado de emissão de documento fiscal; 02 - Com emissão de documento fiscal autorizado pelo município; 03 - Sem emissão de documento fiscal embora obrigado)
		Buffer = Buffer + ConverteTexto ( doc.Serie(0), 5 )											'Série do Documento
		Buffer = Buffer + ConverteValor ( doc.NFiscal(0), 0, 12 )									'Número do Documento
		Buffer = Buffer + ConverteData ( doc.DataEntrega(0), 8)										'Data da Prestação dos Serviços
		Buffer = Buffer + doc.SituacaoNFTS(0)														'Situação da NFTS (N - Normal; C - Cancelado)
		Buffer = Buffer + doc.Tributacao(0)															'Tributação do Serviço
		Buffer = Buffer + ConverteValor ( Round( doc.TotProdutos(0), 2) , 2, 15 )					'Valor dos Serviços
		Buffer = Buffer + ConverteValor ( 0, 2, 15 )												'Valor das Deduções
		Buffer = Buffer + ConverteValor ( doc.CodigoServico(0), 0, 5 )								'Código do Serviço Tomado
		Buffer = Buffer + ConverteValor ( RetiraSinais(doc.CodigoSubitem(0)), 0, 4 )				'Código do Subitem da Lista
		Buffer = Buffer + ConverteValor ( doc.AliquotaServico(0) * 100, 2, 4 )						'Alíquota
		If doc.ISSRF(0) > 0 Then																	'ISS Retido (1 - ISS Retido pelo tomador; 2 - NFTS sem ISS Retido; 3 - ISS Retido pelo intermediário; 4 - ISS Retido pelo tomador (descumprimento d Art. 8º A da Lei Complementar 116); 5 - ISS Retido pelo intermediário (descumprimento d Art. 8º A da Lei Complementar 116))
			Buffer = Buffer + "1"
		Else
			Buffer = Buffer + "2"
		End If
		If Trim(UCase(prestadorServico(8))) <> "BRASIL" And Trim(UCase(prestadorServico(8))) <> "BRAZIL" Then
			Buffer = Buffer + "3"																	'Prestador estabelecido no exterior
			Buffer = Buffer + ConverteValor ( 0, 0, 14 )											'CPF/CNPJ do Prestador
		Else
			If prestadorServico(11) <> "CPF" Then
				Buffer = Buffer + "2"																'CNPJ do Prestador
				Buffer = Buffer + ConverteValor ( RetiraSinais(prestadorServico(6)), 0, 14 )
			Else
				Buffer = Buffer + "1"																'CPF do Prestador
				Buffer = Buffer + ConverteValor ( RetiraSinais(prestadorServico(12)), 0, 14 )
			End If
		End If
		If UCase(Trim(prestadorServico(3))) = "SÃO PAULO" And IsNumeric(RetiraSinais(prestadorServico(13))) Then
			Buffer = Buffer + ConverteValor ( RetiraSinais(prestadorServico(13)), 0, 8 )			'Inscrição Municipal do Prestador (CCM no município de SP)
			Buffer = Buffer + ConverteTexto ( prestadorServico(0), 75 )								'Nome
			Buffer = Buffer + ConverteTexto ( "", 3 )												'Tipo de Logradouro
			Buffer = Buffer + ConverteTexto ( "", 50 )												'Logradouro
			Buffer = Buffer + ConverteTexto ( "", 10 )												'Número
			Buffer = Buffer + ConverteTexto ( "", 30 )												'Complemento
			Buffer = Buffer + ConverteTexto ( "", 30 )												'Bairro
			Buffer = Buffer + ConverteTexto ( "", 50 )												'Cidade
			Buffer = Buffer + ConverteTexto ( "", 2 )												'UF
			Buffer = Buffer + ConverteValor ( 0, 0, 8 )												'CEP
		Else
			Buffer = Buffer + ConverteValor ( 0, 0, 8 )												'Inscrição Municipal do Prestador (Não informar o CCM fora do município de SP)
			Buffer = Buffer + ConverteTexto ( prestadorServico(0), 75 )								'Nome
			Dim pos As Integer, tipo As String, endereco As String
			endereco = Trim(prestadorServico(1))
			tipo = Left(endereco, 3)
			pos = InStr(endereco, " ")
			endereco = Mid(endereco, pos + 1, Len(endereco))
			Buffer = Buffer + ConverteTexto ( tipo, 3 )												'Tipo de Logradouro
			Buffer = Buffer + ConverteTexto ( endereco, 50 )										'Logradouro
			Buffer = Buffer + ConverteTexto ( prestadorServico(9), 10 )								'Número
			Buffer = Buffer + ConverteTexto ( prestadorServico(10), 30 )							'Complemento
			Buffer = Buffer + ConverteTexto ( prestadorServico(2), 30 )								'Bairro
			Buffer = Buffer + ConverteTexto ( prestadorServico(3), 50 )								'Cidade
			Buffer = Buffer + ConverteTexto ( prestadorServico(4), 2 )								'UF
			Buffer = Buffer + ConverteValor ( RetiraSinais(prestadorServico(5)), 0, 8 )				'CEP
		End If
		Buffer = Buffer + ConverteTexto ( prestadorServico(14), 75 )								'Email
		Buffer = Buffer + "1"																		'Tipo de NFTS: 1 - Nota Fiscal do Tomador
		If prestadorServico(15) = "MEI" Then														'Regime de Tributação (0 - Normal; 4 - Simples Nacional; 5 - Microempreendedor Individual MEI)
			Buffer = Buffer + "5"
		ElseIf prestadorServico(15) = "Simples Nacional" Then
			Buffer = Buffer + "4"
		Else
			Buffer = Buffer + "0"
		End If
		Buffer = Buffer + ConverteTexto ( "", 8)													'Data da Pagamento da Nota
		Tamanho = Len( Buffer ) 																	'Contabiliza tamanho total do Buffer
		If Tamanho <> 440 Then GoTo fim
		'****** Discriminação dos Serviços ******
		Dim produto As Variant, valor As Variant
		For scan = 0 To 49
			produto = doc.GetItemValue("Produto_" & scan)
			If produto(0) <> "" Then
				valor = doc.GetItemValue("CusTot_" & scan)
				'Buffer = Buffer & "|" & Left(ConverteTexto(produto(0), 165), 165) & " " & Format(Round(valor(0), 2) , "Standard")
				Buffer = Buffer & "|" & Left(ConverteTexto(produto(0), 200), 200) & " " & Format(Round(valor(0), 2) , "Standard")
			End If
		Next
		Buffer = Buffer & "|||"
		Buffer = Buffer + ConverteTexto ( "", 2 )													'Fim de Linha
		Print #FileNumber, Buffer																	'Salva registro no arquivo
		tot = tot + Round(doc.TotProdutos(0), 2)
		cont = cont + 1
		Set doc = dc.GetNextDocument(doc)
	Loop
	
	Buffer = ""																						'Limpando a variável
	'********************************* Registro Tipo 9 - Rodapé *************************************************************************************
	Buffer = Buffer + "9"																			'Tipo do Registro
	Buffer = Buffer + ConverteValor ( dc.Count, 0, 7 )												'Número de Linhas do Detalhe
	Buffer = Buffer + ConverteValor ( Round(tot, 2), 2, 15 ) 										'Valor Total dos Serviços no Arquivo
	Buffer = Buffer + ConverteValor ( 0, 2, 15 )													'Valor Total das Deduções no Arquivo
	Buffer = Buffer + ConverteTexto ( "", 2 )														'Caractere de Fim de Linha
	Print #FileNumber, Buffer																		'Valor Total da Deduções no Arquivo
	Tamanho = Len( Buffer )																			'Contabiliza tamanho total do Buffer
	'*******************************************************************************************************************************************
	If Tamanho <> 40 Then GoTo fim
	MsgBox ( "Numero de documentos processados: " & cont)
	Close FileNumber
	'Salva os documentos processados, quando o arquivo foi gerado com sucesso
	Set doc = dc.GetFirstDocument
	Do Until doc Is Nothing
		doc.SituacaoEnvioNFTS = "OK"
		doc.ResponsavelEnvioNFTS = session.CommonUserName
		doc.DataEnvioNFTS = Now
		Call doc.save (True, True)
		Set doc = dc.GetNextDocument( doc )
	Loop
	MsgBox "Arquivo gerado com sucesso: " + ArqName
	Call ws.CurrentView.DeselectAll
	Call ws.ViewRefresh
	Exit Sub
fim:
	Kill ArqName
	MsgBox "O arquivo não foi gerado"
	
End Sub

'++LotusScript Development Environment:2:1:ckEmpresa:1:8
Function ckEmpresa (empresa As NotesDocument) As Variant
	
	Dim ck(1) As Variant
	Dim msg As String
	
	ck(0) = True
	If empresa.Nome(0) = "" Then
		msg = Chr(10) + "Razão Social"
		ck(0) = False
	End If
	If empresa.Endereco(0) = "" Then
		msg = msg + Chr(10) + "Endereço"
		ck(0) = False
	End If
	If empresa.Numero(0) = "" Then
		msg = msg + Chr(10) + "Número"
		ck(0) = False
	End If
	If empresa.Telefones(0) = "" Then
		msg = msg + Chr(10) + "Telefones"
		ck(0) = False
	End If
	If empresa.Bairro(0) = "" And (empresa.Pais(0) = "Brasil" Or empresa.Pais(0) = "Brazil") Then
		msg = msg + Chr(10) + "Bairro"
		ck(0) = False
	End If
	If empresa.Cidade(0) = "" Then
		msg = msg + Chr(10) + "Cidade"
		ck(0) = False
	End If
	If empresa.Estado(0) = "" Then
		msg = msg + Chr(10) + "Estado"
		ck(0) = False
	End If
	If empresa.Pais(0) = "" Then
		msg = msg + Chr(10) + "País"
		ck(0) = False
	End If
	If empresa.Cep(0) = "" Then
		msg = msg + Chr(10) + "Cep"
		ck(0) = False
	End If
	If empresa.CGC(0) = "" And (empresa.Pais(0) = "Brasil" Or empresa.Pais(0) = "Brazil") Then
		msg = msg + Chr(10) + "CNPJ"
		ck(0) = False
	End If
	If empresa.Pais(0) = "Brasil" Or empresa.Pais(0) = "Brazil" Then
		If empresa.Hasitem("IndicadorIE") Then
			If (empresa.IndicadorIE(0) = "--" Or empresa.IndicadorIE(0) = "1") And empresa.Inscricao(0) = "" Then
				msg = msg + Chr(10) + "Inscrição Estadual"
				ck(0) = False
			End If
		Else
			If empresa.Inscricao(0) = "" And (empresa.Pais(0) = "Brasil" Or empresa.Pais(0) = "Brazil") Then
				msg = msg + Chr(10) + "Inscrição Estadual"
				ck(0) = False
			End If
		End If
	End If
	ck(1) = msg
	ckEmpresa = ck
	
End Function

'++LotusScript Development Environment:2:1:GetDadosEmpresa:1:8
Function GetDadosEmpresa(codigo As String) As Variant
	
	Dim session As New NotesSession
	Dim db As New NotesDatabase( session.CurrentDatabase.Server, "empresas.nsf" )
	Dim view As NotesView
	Dim doc As NotesDocument
	Dim array(15) As String
	Set view = db.GetView( "(Dados Cadastrais)" )
	Set doc = view.GetDocumentByKey( codigo, True )
	If Not doc Is Nothing Then
		array(0) = doc.Nome(0)				'Nome
		array(1) = doc.Endereco(0)			'Endereço
		array(2) = doc.Bairro(0)			'Bairro
		array(3) = doc.Cidade(0)			'Cidade
		array(4) = doc.Estado(0)			'UF
		array(5) = doc.Cep(0)				'CEP
		array(6) = doc.CGC(0)				'CNPJ
		array(7) = doc.Inscricao(0)			'Inscrição Estadual
		array(8) = doc.Pais(0)				'País
		array(9) = doc.Numero(0)			'Número
		array(10) = doc.Complemento(0)		'Complemento
		array(11) = doc.TipoInscricao(0)	'Indicador do CPF/CNPJ do Prestador
		array(12) = doc.CPF(0)				'CNPJ
		array(13) = doc.CCM(0)				'Inscricao Municipal
		array(14) = doc.EmailNFeServicos(0)	'Email
		array(15) = doc.RegimeTributario(0)	'Regime Tributário
	End If
	GetDadosEmpresa = array
	
End Function


'++LotusScript Development Environment:2:1:ConverteData:1:8
Function ConverteData ( ByVal Data As Variant, ByVal Tamanho As Integer ) As String
'Converte datas para o formato DDMM, DDMMAA ou AAAAMMDD, AAAA-MM-DD, conforme o valor Tamanho ( 4, 6, 8 ou 10 )
	
	Dim dia As String, mes As String, ano As String
	dia = Day ( Data ) 
	If Len(dia) = 1 Then
		dia = "0" + CStr ( dia )
	Else
		dia = CStr ( dia )
	End If
	
	mes = Month ( Data )
	If Len(mes) = 1 Then
		mes = "0" + CStr ( mes )
	Else
		mes = CStr ( mes )
	End If
	
	ano = CStr ( Year ( Data ) )
	If ( Tamanho = 6 ) Then
		ano = Right ( ano, 2 )
	End If
	
	If Tamanho = 4 Then
		ConverteData = dia + mes
	ElseIf Tamanho = 6 Then
		ConverteData = dia + mes + ano
	ElseIf Tamanho = 8 Then
		ConverteData = ano + mes + dia
	Else
		ConverteData = ano + "-" + mes + "-" + dia
	End If
	
End Function

'++LotusScript Development Environment:2:1:ConverteValor:1:8
Function ConverteValor ( ByVal Valor As Variant, ByVal Casas As Integer, ByVal Tamanho As Integer ) As String
'Converte um valor númerico de forma que ele fique no formato 000IIIDD, de acordo com o Tamanho de Entrada
'Os 0s à esquerda são acrescentados caso o valor formatado não possua o Tamanho entrado.
'I se refere à parte inteira do Valor. D se refere à parte decimal do Valor.
'Retorna o valor formatado adequadamente
	
	Dim Saida As String
	Dim Aux As String
	Dim Inteira As String
	Dim Fracionaria As String
	Dim Pos As Integer
	Dim i As Integer
	Aux = CStr ( Valor )
	Pos = InStr ( 1, Valor, "," )
	If Pos > 0 Then
		Inteira = Left ( Aux, Pos - 1 )
		Fracionaria = Mid ( Aux, Pos + 1 )
		If (Len(Inteira) + Casas ) > Tamanho Then 
			MsgBox ("Estouro. Possível perda de dados -> " + Aux)
			Stop
			ConverteValor = ""
			Exit Function
		End If
		If  Len ( Fracionaria ) > Casas  Then
			MsgBox ("Estouro. Criar arredondamento ? -> " + Aux)
			Stop
			ConverteValor = ""
			Exit Function
		End If
		For i = 1 To Casas - Len ( Fracionaria )
			Fracionaria = Fracionaria + CStr ( 0 )
		Next
	Else
		Inteira = Aux
		Fracionaria = ""
		If (Len(Inteira) + Casas ) > Tamanho Then 
			MsgBox ("Estouro. Possível perda de dados -> " + Aux)
			Stop
			ConverteValor = ""	
			Exit Function
		End If
		For i = 1 To Casas
			Fracionaria = Fracionaria + CStr ( 0 )
		Next
	End If
	
	Saida = ""
	For i = 1 To Tamanho - ( Len ( Inteira) + Len ( Fracionaria ) )
		Saida = Saida + "0"
	Next
	Saida = Saida + Inteira + Fracionaria
	ConverteValor = Saida
	
End Function
