'++LotusScript Development Environment:2:5:(Options):0:74
%REM
	Library SintegraCompras
	Created Feb 18, 2011 by Ludimila Oliveira/TDec
	Description: Comments for Library
%END REM
Option Public
Option Declare


'++LotusScript Development Environment:2:5:(Forward):0:1
Declare Function ConverteTexto ( ByVal Texto As String, ByVal Tamanho As Integer ) As String
Declare Function ConverteValor ( ByVal Valor As Variant, ByVal Casas As Integer, ByVal Tamanho As Integer ) As String
Declare Function GetDadosEmpresa(codigo As String) As Variant
Declare Function GetEquipamento(partN As String) As Variant
Declare Function RetiraSinais ( texto As String) As String
Declare Function ConverteData ( ByVal Data As Variant, ByVal Tamanho As Integer ) As String
Declare Sub GeraArquivoSintegra
Declare Function getCodigo As Variant

'++LotusScript Development Environment:2:5:(Declarations):0:10
Dim arqName As String
Dim FileNumber As Integer
Dim Buffer As String
Dim DataHoje As Variant
Dim Tamanho As Long
Dim doc As NotesDocument
Dim empresa As Variant
Dim ckCodigo As Boolean

'++LotusScript Development Environment:2:1:ConverteTexto:1:8
Function ConverteTexto ( ByVal Texto As String, ByVal Tamanho As Integer ) As String
'Converte o Texto entrado, de forma que ele se adeque ao formato apropriado. Caso o Texto não possua o 
'Tamanho entrado, espaços em branco são acrescidos ao final do Texto, até que o tamanho seja atingido
'Remove os acentos e "tils" caso existam e retorna o Texto formatado.
	
	Dim i As Integer
	Dim Size As Integer
	
	Texto = Trim( Texto )
	Size = Len ( Texto )
	
	For i = 1 To Size								'Substitui caracteres inválidos
		Select Case Mid ( Texto, i, 1 )
		Case "Á"
			Mid ( Texto, i, 1 ) = "A"
		Case "Â"
			Mid ( Texto, i, 1 ) = "A"
		Case "À"
			Mid ( Texto, i, 1 ) = "A"
		Case "Ã"
			Mid ( Texto, i, 1 ) = "A"
		Case "É"
			Mid ( Texto, i, 1 ) = "E"
		Case "Ê"
			Mid ( Texto, i, 1 ) = "E"
		Case "Í"
			Mid ( Texto, i, 1 ) = "I"
		Case "Ó"
			Mid ( Texto, i, 1 ) = "O"
		Case "Ô"
			Mid ( Texto, i, 1 ) = "O"
		Case "Õ"
			Mid ( Texto, i, 1 ) = "O"
		Case "Ú"
			Mid ( Texto, i, 1 ) = "U"
		Case "Ç"
			Mid ( Texto, i, 1 ) = "C"
		Case "º"
			Mid ( Texto, i, 1 ) = " "
		Case "&"
			Mid ( Texto, i, 1 ) = "E"
		Case "$"
			Mid ( Texto, i, 1 ) = " "
		Case Chr(10)
			Mid ( Texto, i, 1 ) = " "
			Mid ( Texto, i - 1, 1 ) = " "
		End Select
	Next
	
	If Size <=Tamanho Then						'Verifica se não há estouro de tamanho
		i = Size + 1
		For i = i To Tamanho
			Texto = Texto + " "	
		Next
		
	Else															'Há estouro. Erro.
		MsgBox ("Estouro. Texto entrado é maior que o tamanho especificado. -> " + Texto)
		Stop
		ConverteTexto = ""
		Exit Function
	End If
	
	ConverteTexto = Texto
	
End Function


'++LotusScript Development Environment:2:1:ConverteValor:1:8
Function ConverteValor ( ByVal Valor As Variant, ByVal Casas As Integer, ByVal Tamanho As Integer ) As String
	
	Dim Saida As String									'Valor inteiro convertido
	Dim Aux As String
	Dim Inteira As String									'Parte inteira do valor ( antes da vírgula )
	Dim Fracionaria As String							'Parte fracionaria do valor ( depois da vírgula )
	Dim Pos As Integer
	Dim i As Integer
	
	If CStr(Valor) = "" Then Valor = 0
	Valor = Round(Valor, Casas)
	Aux = CStr ( Valor )	
	Pos = InStr ( 1, Valor, "," )
	If Pos > 0 Then										'Existe virgula
		Inteira = Left ( Aux, Pos - 1 )
		Fracionaria = Mid ( Aux, Pos + 1 )
		If (Len(Inteira) + Casas ) > Tamanho Then 
			MsgBox ("Estouro. Possível perda de dados -> " + Aux)
			Stop
			ConverteValor = ""
			Exit Function
		End If
		If  Len ( Fracionaria ) > Casas  Then
			MsgBox ("Estouro. Criar arredondamento ? -> " + Aux)
			Stop
			ConverteValor = ""
			Exit Function
		End If
		For i = 1 To Casas - Len ( Fracionaria )
			Fracionaria = Fracionaria + CStr ( 0 )
		Next
	Else
		Inteira = Aux
		Fracionaria = ""
		If (Len(Inteira) + Casas ) > Tamanho Then 
			MsgBox ("Estouro. Possível perda de dados -> " + Aux)
			Stop
			ConverteValor = ""	
			Exit Function
		End If
		For i = 1 To Casas
			Fracionaria = Fracionaria + CStr ( 0 )
		Next
	End If
	
	Saida = ""
	For i = 1 To Tamanho - ( Len ( Inteira) + Len ( Fracionaria ) )
		Saida = Saida + "0"
	Next
	Saida = Saida + Inteira + Fracionaria
	ConverteValor = Saida
	
End Function


'++LotusScript Development Environment:2:1:GetDadosEmpresa:1:8
Function GetDadosEmpresa(codigo As String) As Variant
	
	Dim session As New NotesSession
	Dim db As New NotesDatabase( session.CurrentDatabase.Server, "empresas.nsf" )
	Dim view As NotesView
	Dim doc As NotesDocument
	Dim array(5) As String
	Set view = db.GetView( "(Dados Cadastrais)" )
	Set doc = view.GetDocumentByKey( codigo, True )
	If Not doc Is Nothing Then
		array(0) = doc.Nome(0)				'Nome
		array(1) = doc.Inscricao(0)			'Inscrição Estadual
		array(2) = doc.Cidade(0)			'Cidade
		array(3) = doc.CGC(0)				'CNPJ
		array(4) = doc.Estado(0)			'UF
		array(5) = doc.TipoInscricao(0)		'Tipo de Inscrição (Pessoa Física ou Pessoa Jurídica)
	End If
	GetDadosEmpresa = array
	
End Function


'++LotusScript Development Environment:2:1:GetEquipamento:5:8
%REM
	Function GetEquipamento
	Description: Comments for Function
%END REM
Function GetEquipamento(partN As String) As Variant
	
	Dim session As New NotesSession
	Dim db As New NotesDatabase(session.Currentdatabase.Server, "estoquemain.nsf")
	Dim view As NotesView
	Dim doc As NotesDocument
	Dim array(5) As Variant
	Set view = db.GetView("(Catalogo/PartNumber)")
	Set doc = view.Getdocumentbykey(partN, True)
	If Not doc Is Nothing Then
		array(0) = doc.Origem(0) & doc.ST(0) 'CST
		array(1) = doc.CF(0) 'NCM
		array(2) = doc.Nome(0) 'Descrição
		array(3) = doc.Unidade(0) 'UM
		array(4) = doc.IPI(0) 'Alíquota do IPI
		array(5) = doc.ICMS(0) 'Alíquota do ICMS
	End If
	GetEquipamento = array
	
End Function


'++LotusScript Development Environment:2:1:RetiraSinais:1:8
Function RetiraSinais ( texto As String) As String
	
	Dim i As Integer
	Dim NovoTexto As String
	Dim size As Long
	
	Texto = UCase ( Texto )
	Size = Len ( Texto )
	
	For i = 1 To Size								'Retira caracteres inválidos
		Select Case Mid ( Texto, i, 1 )
		Case "."
		Case "-"
		Case "/"
		Case "R"
		Case "$"
		Case ":"
		Case ";"
		Case " "
		Case Else
			NovoTexto = NovoTexto + Mid ( Texto, i, 1 )
		End Select
	Next
	
	RetiraSinais = NovoTexto
	
End Function



'++LotusScript Development Environment:2:1:ConverteData:1:8
Function ConverteData ( ByVal Data As Variant, ByVal Tamanho As Integer ) As String
'Converte datas para o formato DDMMAA
	
	Dim dia As String, mes As String, ano As String
	dia = Day ( Data ) 
	If Len(dia) = 1 Then
		dia = "0" + dia
	End If
	mes = Month ( Data )
	If Len(mes) = 1 Then
		mes = "0" + mes	
	End If
	Ano = CStr ( Year ( Data ) )
	If ( Tamanho = 6 ) Then
		ano = Right ( Ano, 2 )
	End If
	
	If Tamanho = 8 Then
		ConverteData = ano + mes + dia
	ElseIf Tamanho = 10 Then
		ConverteData = dia + "/" + mes + "/"+ ano
	ElseIf Tamanho = 42 Then
		ConverteData = ano + mes
	ElseIf Tamanho = 224 Then
		ConverteData = dia + mes + ano
	Else
		ConverteData = dia + mes + ano
	End If
	
End Function


'++LotusScript Development Environment:2:2:GeraArquivoSintegra:5:8
%REM
	Sub GeraArquivoSintegra
	Description: Comments for Sub
%END REM
Sub GeraArquivoSintegra
	
	Dim ws As New NotesUIWorkspace
	Dim uiview As NotesUIView
	Dim dc As NotesDocumentCollection
	Dim codEmpresa As String
	Dim scan As Integer, x As Integer, cont As Integer
	Dim array As Variant, array2 As Variant, equipamento As Variant, indice As Variant, codigo As Variant
	Dim data As NotesDateTime
	ReDim Preserve partN(x) As String
	Set uiview = ws.CurrentView
	Set dc = uiview.Documents
	If dc.Count = 0 Then
		MsgBox "É necessário selecionar pelo menos 1 Documento"
		Exit Sub
	End If
	
	Set doc = dc.GetFirstDocument
	codEmpresa = doc.CodEmpresa(0)
	DataHoje = Now
	FileNumber = FreeFile ( )
	
	'Nome do Arquivo
	arqName = "C:\Sintegra\" & doc.CodEmpresa(0) & "\NFE" & ConverteData ( doc.DataEmissaoNF(0), 42 ) & "A.TXT"
	Open ArqName For Output As FileNumber
	ckCodigo = True
	
	'********************************* Registro Tipo 54 - Mercadoria/Produto *********************************
	Do Until doc Is Nothing
		If CStr(doc.Form(0)) <> "Troca de Reserva" Then
			If CStr(doc.TipoBaixa(0)) <> "Pré-Baixa" Then
				If CStr(doc.NFiscal(0)) <> "" Then
					If doc.Tipo(0) = "ICMS" Then
						For scan = 0 To 49
							array = doc.Getitemvalue("PartNo_" & scan)
							If array(0) <> "" Then
								'Obtém PartN para utilizar no Registro Tipo 75
								If x = 0 Then
									ReDim Preserve partN(x) As String
									partN(x) = array(0)
									x = x + 1
								Else
									indice = ArrayGetIndex(partN, array(0))
									If IsNull(indice) Then
										ReDim Preserve partN(x) As String
										partN(x) = array(0) 
										x = x + 1 
									End If
								End If
								Buffer = ""
								Buffer = Buffer + "54" 'Tipo
								'Busca Dados do Fornecedor em Empresas
								empresa = GetDadosEmpresa(doc.CodCliente(0))
								Buffer = Buffer + ConverteTexto (RetiraSinais (empresa(3)), 14) 'CNPJ
								Buffer = Buffer + ConverteValor ( 1, 0, 2) 'Modelo
								Buffer = Buffer + ConverteTexto ("", 3) 'Série
								Buffer = Buffer + ConverteValor ( doc.NFiscal(0), 0, 6) 'Número da Nota Fiscal
								codigo = getCodigo
								Buffer = Buffer + ConverteValor ( RetiraSinais(codigo(2)), 0, 4) 'CFOP
								array = doc.Getitemvalue("ST_" & scan)
								If CStr(array(0)) <> "" Then
									Buffer = Buffer + ConverteTexto (array(0), 3) 'CST
								Else
									array = doc.Getitemvalue("IC_" & scan)
									If array(0) > 0 Then
										Buffer = Buffer + "000" 'CST
									Else
										Buffer = Buffer + "060" 'CST
									End If
								End If
								Buffer = Buffer + ConverteValor ( scan + 1, 0, 3) 'Número do Item na Nota Fiscal
								array = doc.Getitemvalue("PartNo_" & scan)
								Buffer = Buffer + ConverteTexto (Left(array(0), 14), 14) 'PartN
								array = doc.Getitemvalue("Q_" & scan)
								Buffer = Buffer + ConverteValor ( array(0), 3, 11) 'Quantidade
								array = doc.Getitemvalue("CusTot_" & scan)
								Buffer = Buffer + ConverteValor ( array(0), 2, 12) 'Valor Total do Produto
								Buffer = Buffer + ConverteValor ( 0, 2, 12) 'Desconto
								array = doc.Getitemvalue("IC_" & scan)
								If array(0) > 0 Then
									array = doc.Getitemvalue("CusTot_" & scan)
									Buffer = Buffer + ConverteValor ( array(0), 2, 12) 'Base de Cálculo do ICMS
								Else
									Buffer = Buffer + ConverteValor ( 0, 2, 12) 'Base de Cálculo do ICMS
								End If
								Buffer = Buffer + ConverteValor ( 0, 2, 12) 'Base de Cálculo do ICMS-ST
								array = doc.Getitemvalue("IP_" & scan)
								array2 = doc.Getitemvalue("CusTot_" & scan)
								Buffer = Buffer + ConverteValor ( array(0) * array2(0), 2, 12) 'Valor do IPI
								array = doc.Getitemvalue("IC_" & scan)
								Buffer = Buffer + ConverteValor ( array(0) * 100, 2, 4) 'Alíquota do ICMS
								Print #FileNumber, Buffer
								Tamanho = Len( Buffer )
								If Tamanho <> 126 Then GoTo Erro
							End If
						Next
					End If
				End If	
			End If
		End If
		cont = cont + 1
		Print "Documentos Processados: " & cont
		Set doc = dc.GetNextDocument(doc)
	Loop
	
	'********************************* Registro Tipo 75 - Descrição Mercadoria/Produto *********************************
	Set doc = dc.GetFirstDocument 'Utilizado para calcular a Data Inicial e Final
	For scan = 0 To UBound(partN)
		'Busca Dados no Catálogo do Estoque Main
		equipamento = GetEquipamento(partN(scan))
		Buffer = ""
		Buffer = Buffer + "75" 'Tipo
		Set data = New NotesDateTime("01/" & Month(doc.DataEmissaoNF(0)) & "/" & Year(doc.DataEmissaoNF(0)))
		Buffer = Buffer + ConverteData ( data.DateOnly, 8 ) 'Data Inicial
		Call data.Adjustmonth(1)
		Call data.Adjustday(-1)
		Buffer = Buffer + ConverteData ( data.DateOnly, 8 ) 'Data Final
		Buffer = Buffer + ConverteTexto ( Left(partN(scan), 14), 14) 'PartN
		Buffer = Buffer + ConverteTexto ( equipamento(1), 8) 'NCM
		Buffer = Buffer + ConverteTexto ( Left(equipamento(2), 53), 53) 'Descrição
		Buffer = Buffer + ConverteTexto ( Left(equipamento(3), 6), 6) 'UM
		Buffer = Buffer + ConverteValor ( equipamento(4) * 100, 2, 5) 'Alíquota do IPI
		Buffer = Buffer + ConverteValor ( equipamento(5) * 100, 2, 4) 'Alíquota do ICMS
		Buffer = Buffer + ConverteValor ( 0, 2, 5) 'Redução da Base de Cálculo do ICMS
		Buffer = Buffer + ConverteValor ( 0, 2, 13)'Base de Cálculo do ICMS-ST
		Print #FileNumber, Buffer
		Tamanho = Len( Buffer )
		If Tamanho <> 126 Then GoTo Erro
	Next
	Close FileNumber
	MsgBox "Arquivo gerado com sucesso: " + ArqName
	Exit Sub
		
erro:
	Close FileNumber
	Kill ArqName
	MsgBox "Erro no Tamanho do Arquivo. O arquivo não foi gerado"
	Exit Sub
		
End Sub

'++LotusScript Development Environment:2:1:getCodigo:1:8
Function getCodigo As Variant
	
	Dim session As New NotesSession
	Dim db As NotesDatabase
	Dim view As NotesView
	Dim config As NotesDocument
	Dim key(2) As String, array(3) As String
	
	Set db = session.CurrentDatabase
	key(0) = doc.TipoCompra(0)
	key(1) = doc.Natureza(0)
	key(2) = doc.Classificacao(0)
	Set view = db.GetView("(Configuracao/Codigos)")
	Set config = view.GetDocumentByKey(key, True)
	If Not config Is Nothing Then
		array(0) = config.Codigo(0)
		array(1) = config.Descricao(0)
	Else
		array(0) = ""
		array(1) = ""
		array(3) = ""
	End If
	
	If doc.TipoCompra(0) = "Consumo" Then
		If empresa(4) = "SP" Then
			array(2) = "1.556"
		Else
			array(2) = "2.556"
		End If
	ElseIf doc.TipoCompra(0) = "Ativo" Then
		If empresa(4) = "SP" Then
			array(2) = "1.551"
		Else
			array(2) = "2.551"
		End If
	Else	'Revenda
		If empresa(4) = "SP" Then
			array(2) = "1.102"
		Else
			array(2) = "2.102"
		End If
	End If
	getCodigo = array
	
End Function